import pandas as pd
import os 

# Before publication, re-run the whole thing to make sure there is no unfinshed jobs. 

configfile: "config/config.yml"

samples=[ff.replace('.faa','') for ff in os.listdir(config['DIR_faa']) if ff.endswith('.faa')]
# samples=samples[:2] # for testing
carbons=['glc__D','succ','arab__L','drib','glcur','glyc','mnl','man','melib','raffin','but','ppa']

print(f"Found {len(samples)} samples: ", samples)

rule all:
    input:
        expand(os.path.join(config['DIR_CARVEME'],"{sample}-{carbon}_gapfill.xml"),sample=samples,carbon=carbons), # gap-filled carveme models
        expand(os.path.join(config['DIR_FBA_PREDICTION'],'{sample}-{carbon}_gapfill.csv'),sample=samples,carbon=carbons), # fba predictions
        expand(os.path.join(config['DIR_FBA_PREDICTION'],'{sample}-{carbon}_gapfill_force_uptake.csv'),sample=samples,carbon=carbons), 
        

rule carveme:
    input:
        faa=os.path.join(config["DIR_faa"],'{sample}.faa')
    output:
        model_gapfill=os.path.join(config['DIR_CARVEME'],'{sample}-{carbon}_gapfill.xml')
    params:
        media='M9[{carbon}]',
        media_file=config['FILE_media']
    conda: 'fba'
    threads:
        config['threads']
    shell:
        """
        export PYTHONPATH=/home/zeqianli/scratch-midway3/bin/cplex/cplex/python/3.9/x86-64_linux 
        carve {input.faa} --output {output.model_gapfill} -g {params.media} -i {params.media} --mediadb {params.media_file}
        """
        # I wish I don't need to add the first line but UChicago cluster is a piece of fucking garbage and this is the only way I can make it work. 


rule run_fba:
    input:
        model_gapfill=os.path.join(config['DIR_CARVEME'],'{sample}-{carbon}_gapfill.xml')
    output:
        growth_gapfill=os.path.join(config['DIR_FBA_PREDICTION'],'{sample}-{carbon}_gapfill.csv'),
        growth_gapfill_force_uptake=os.path.join(config['DIR_FBA_PREDICTION'],'{sample}-{carbon}_gapfill_force_uptake.csv')
    params:
        initial_medium=config['FILE_INITIAL_MEDIUM']
    conda: 'fba'
    threads: 1
    shell:
        """ 
        python scripts/run_fba.py --output {output.growth_gapfill} --initial_medium {params.initial_medium} {input.model_gapfill}
        python scripts/run_fba.py --output {output.growth_gapfill_force_uptake} --force_uptake --initial_medium {params.initial_medium} {input.model_gapfill}
        """
